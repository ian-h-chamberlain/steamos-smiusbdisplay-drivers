# Maintainer: Ian Chamberlain <https://github.com/ian-h-chamberlain>
_pkgname=SMIUSBDisplay-driver
pkgname="${_pkgname,,}-bin"
pkgver=2.12.0.0
pkgrel=1
pkgdesc="Driver for SiliconMotion USB display devices"
arch=('i686' 'x86_64')
url="https://hypershop.zendesk.com/hc/en-us/articles/10344404131988-Linux-Driver-Download"
license=('custom')
depends=(evdi libdrm libusb 'bash>=4')
source=("https://hypershop.zendesk.com/hc/en-us/article_attachments/10344352272532/$_pkgname.$pkgver.run"
        "0001-Remove-unecessary-checks-for-requirements.patch"
        "0002-Actually-enable-check-usage-verify-preconditions.patch"
        "0003-Don-t-prompt-for-reboot-after-install.patch"
        "0004-Support-DESTDIR-var-for-non-root-installation.patch"
        "0005-Fix-lib-usr-lib-to-match-packaging-standards.patch"
        "0006-Don-t-install-system-libusb.patch")
sha256sums=('859e3faf69171a28dcabfb0cc2a1e284db41a18c5bc0d4265420244f2cdd2331'
            'd66f694b1d1b6cfff1caf39d95de673dfc63756f1d465888d8fb98d19e5d0aed'
            '98e47fdd10dfde3b3897ebcc1d5086ef7a85111f7d8f267a5dfefdbdc4db29dd'
            'e2b2a7b4cb0d215da7e95bac66fe1f0ef39490362350481732bb2cebaf9a7d4e'
            '19a91d85d063b239d7255c1d12c6fd2329dde43314b0dcc7b42c77e76917ef92'
            'f9b7b72bda9ec5e01d367e8fda781428e50244cd80bfeec2b44b6d493b3d9e8e'
            '04ec007073ef8af94607de630e7c964010bccb9837ed506ac572dc075f4d5b1e')
noextract=("$_pkgname.$pkgver.run")

prepare() {
    # The source archive is a self-extracting shell script, so we can use it
    # directly to extract the contents
    chmod +x "$_pkgname.$pkgver.run"
	"./$_pkgname.$pkgver.run" --target "$srcdir/$pkgname-$pkgver" --noexec --nochown
    cd "$srcdir/$pkgname-$pkgver"
    for patch_file in "$srcdir"/*.patch; do
        patch --forward --strip=1 --input="$patch_file"
    done
}

check() {
    "./$_pkgname.$pkgver.run" --check
    cd "$pkgname-$pkgver"
    ./install.sh check
}

package() {
	cd "$pkgname-$pkgver"

    # Create a couple dirs the install script assumes exist
    mkdir -p "$pkgdir/usr/bin"
    mkdir -p "$pkgdir/usr/lib/systemd/system"

	DESTDIR="$pkgdir" ./install.sh

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
