# Maintainer: Francois Menning <f.menning@pm.me>
# Contributor: mwawrzyniak <arch at cmstactical dot net>
# Contributor: PlusMinus

_pkgname=evdi
pkgname=evdi-steamos-dkms
pkgver=1.14.1
pkgrel=1
pkgdesc="A LinuxÂ® kernel module that enables management of multiple screens."
arch=('i686' 'x86_64')
url="https://github.com/DisplayLink/evdi"
license=('GPL')
depends=(glibc dkms libdrm)
optdepends=('linux-neptune-headers: to build on SteamOS 3.4 (kernel 5.13)'
            'linux-neptune-61-headers: to build on SteamOS 3.5 (kernel 6.1)')
provides=(evdi)
conflicts=(evdi)
source=("https://github.com/DisplayLink/evdi/archive/v$pkgver/$_pkgname-$pkgver.tar.gz"
        "0001-Allow-older-kernel-version-for-p4cc-format.patch"
        "0002-Rename-drm_fb_helper.fbdev-.info.patch")
sha256sums=('a5f1c10602a6219ac4b13d4f590c16e6fb65bed94f28562260c9b481b48c3828'
            '99587702751246146c7318d37fc4de3c11d230440955ee895e59d5f309ffaeea'
            '6574ec9129eded5eea6d1b54e22c8fe587c0e1c3d4855ff9c0d0f47f5c9c00ce')

prepare() {
  cd "$_pkgname-$pkgver"
  patch --forward --strip=1 --input="${srcdir}/${source[1]}"
  patch --forward --strip=1 --input="${srcdir}/${source[2]}"
}

build() {
  # We only need to build the library in this step, dkms will build the module
  cd "$_pkgname-$pkgver/library"
  make
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm0755 library/lib$_pkgname.so "$pkgdir/usr/lib/lib$_pkgname.so"

  SRCDIR="$pkgdir/usr/src/$_pkgname-$pkgver"	# This one is needed for dkms
  install -d "$SRCDIR"
  install -Dm0755 module/* "$SRCDIR"
}
