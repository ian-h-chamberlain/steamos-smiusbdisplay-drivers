# Maintainer: Francois Menning <f.menning@pm.me>
# Contributor: mwawrzyniak <arch at cmstactical dot net>
# Contributor: PlusMinus

_pkgname=evdi
pkgname=evdi-steamos-dkms
pkgver=1.12.0
pkgrel=1
pkgdesc="A LinuxÂ® kernel module that enables management of multiple screens."
arch=('i686' 'x86_64')
url="https://github.com/DisplayLink/evdi"
license=('GPL')
depends=(glibc dkms libdrm)
provides=(evdi)
conflicts=(evdi)
source=("https://github.com/DisplayLink/evdi/archive/v$pkgver/$_pkgname-$pkgver.tar.gz"
        "0001-Allow-older-kernel-version-for-p4cc-format.patch")
sha256sums=('484b1166ac1eccd14ec2c88ba426089d001401ba33612620d38d5f214a809fef'
            '42b7d455ef187199f114fd7479c3f2ee832ed13ca4d37deb5791633a25121b42')

prepare() {
  cd "$_pkgname-$pkgver"
  patch --forward --strip=1 --input="${srcdir}/0001-Allow-older-kernel-version-for-p4cc-format.patch"
}

build() {
  # We only need to build the library in this step, dkms will build the module
  cd "$_pkgname-$pkgver/library"
  make
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm0755 library/lib$_pkgname.so "$pkgdir/usr/lib/lib$_pkgname.so"

  SRCDIR="$pkgdir/usr/src/$_pkgname-$pkgver"	# This one is needed for dkms
  install -d "$SRCDIR"
  install -Dm0755 module/* "$SRCDIR"
}
